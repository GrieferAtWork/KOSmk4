/* Copyright (c) 2019-2022 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2019-2022 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */


import * from deemon;
import * from ..libmagic.libmagic;
import opt = ._options;
import fs;
import ipc;
import net;
import ._common as common;

function enumerateQEmuInstallationLocations(): {string...} {
#ifdef __WINDOWS__
	yield r"D:\qemu";
	yield r"D:\apps\qemu";
#endif /* __WINDOWS__ */
	yield fs.environ["PATH"].split(fs.DELIM)...;
}

function qemuMonitorBackend(addr: string, monitorPipe: File) {
	local s = net.socket("INET", none, none);
	for (local i: [5000]) {
		try {
			s.connect(addr);
			break;
		} catch (e...) {
			if (i == 4999) {
				print Traceback.current;
				print repr e;
			}
		}
		Thread.sleep(milliseconds from time(1));
	}
	Thread(() -> {
		local isFirst = true;
		local final prefix = "monitor>: ";
		for (;;) {
			local text = s.recv();
			if (isFirst) {
				text = prefix + text;
				isFirst = false;
			}
			text = text.replace("\n", "\n" + prefix);
			File.stdout << text;
			File.stdout.sync();
		}
	}).start();
	local isFirst = true;
	local final prefix = "monitor<: ";
	for (;;) {
		local text = monitorPipe.read();
		s.send(text);
		if (isFirst) {
			text = prefix + text;
			isFirst = false;
		}
		text = text.replace("\n", "\n" + prefix);
		File.stdout << text;
		File.stdout.sync();
	}
}


function start(
		groups: {Group...},
		config: Config,
		BINPATH: string,
		KERNEL: string,
		DRIVERS: {(string, string)...},
		TARGET_ARCH: string,
		TARGET: Module,
		DISK_FILENAME: string,
		options: {string: string},
		magic: Module) {
	local qemu = options.get("qemu.exe");
	local qemuArgs = [];
	qemuArgs.extend({ "-serial", "stdio" });
	if (opt.getBool(options, opt.HOSTED, true)) {
		/* NOTE: Qemu will refuse to guest requests for a reboot when
		 *       -no-reboot is given. However, reboot-on-tripple-fault
		 *       will  also  work when  -no-reboot isn't  given, which
		 *       probably isn't something you'd want...
		 * NOTE: When GDB is enabled, you'll get a SIGQUIT trap when
		 *       the guest tries to  reboot, and -no-reboot is  set. */
//		qemuArgs.extend({ "-no-shutdown" });
		qemuArgs.extend({ "-no-reboot", "-no-shutdown" });
	}
	local gdbMode = options.get(opt.GDB);
	local bootDrivers = List(DRIVERS);
	if (gdbMode !is none) {
		local gdbPort = options.get(opt.GDB_PORT, "1234");
		if (gdbMode == "server") {
			/* Make use of out GDB server driver */
			bootDrivers.append((
				BINPATH + "/os/drivers/gdbserver",
				"transport=serial:0x2f8:0xf3"
			));
			qemuArgs.extend({ "-serial", "tcp::" + gdbPort + ",server" });
		} else if (gdbMode == "emulator") {
			/* Use qemu's builtin GDB support */
			/* Make use of out GDB-stub driver */
			if (gdbPort != "1234") {
				qemuArgs.extend({ "-gdb", "tcp::" + gdbPort });
			} else {
				qemuArgs.append("-s");
			}
			qemuArgs.append("-S");
		} else {
			throw Error("Invalid GDB mode: " + repr gdbMode);
		}
	}
	if (TARGET_ARCH == "x86_64") {
		/* Must create+use a custom kernel binary with e_machine = EM_386 */
		local kernel386 = f"{fs.headof(KERNEL)}.em386-{fs.tailof(KERNEL)}";
		local x32modified = getLastModfiedTime(kernel386);
		if (x32modified !is none) {
			local x64modified = getLastModfiedTime(KERNEL);
			if (x64modified !is none && x64modified <= x32modified)
				goto did_update_x86_64_kernel;
			try fs.unlink(kernel386); catch (...);
		}
		print "[qemu] Updating kernel shadow", repr kernel386;
		fs.copyfile(KERNEL, kernel386);
		with (local fp = File.open(kernel386, "rb+,nobuf")) {
			fp.seek(18); /* __OFFSET_ELF64_EHDR_MACHINE */
			fp.write((3).tobytes(2, "little", false)); /* EM_386 */
		}
did_update_x86_64_kernel:
		KERNEL = kernel386;
	}
	if (bootDrivers) {
		qemuArgs.append("-initrd");
		qemuArgs.append(",".join(
			for (local name, cmdline: bootDrivers)
				!cmdline
					? name.replace(",", ",,")
					: f"{name.replace(",", ",,")} {cmdline.replace(",", ",,")}"
		));
	}
	try {
		local final traceFilename = "qemu.events";
		for (local l: File.open(traceFilename, "r")) {
			l = l.strip();
			if (l.startswith("#") || !l)
				continue;
			/* Non-empty trace file */
			qemuArgs.extend({
				"-trace",
				"events=" + traceFilename
			});
			break;
		}
	} catch (...) {
	}
	for (local l: (try File.open("qemu.conf", "r") catch (...) none)) {
		l = l.strip();
		if (l.startswith("#") || !l)
			continue;
		l = l.decode("utf-8");
		local cmd, none, arg = l.partition("=")...;
		switch (cmd) {
		case "qemu":
			if (arg.startswith('"') && arg.endswith('"'))
				arg = arg[1:-1].decode("c-escape");
			qemu = arg;
			if (fs.stat.isdir(qemu))
				qemu = fs.joinpath(qemu, f"qemu-system-{TARGET_ARCH}");
			break;
		case "accel":
			/* "-accel hax" breaks qemu's builtin GDB stub. :(
			 * -> For now, ignore accel options if GDB is on. */
			if (gdbMode != "emulator") {
				qemuArgs.extend({ "-accel", arg });
			}
			break;
		case "xdrive1":
		case "xdrive2":
		case "xdrive3":
			if (!fs.stat.exists(arg))
				break;
			/* FALLTHRU */
		case "drive1":
		case "drive2":
		case "drive3":
			qemuArgs.extend({
				"-drive",
				f"file={arg},format=raw,index={cmd.last},media=disk"
			});
			break;
		case "args":
			qemuArgs.extend(arg.split(" "));
			break;
		default:
			throw Error(f"Unknown qemu conf option: {repr cmd}");
			break;
		}
	}
	qemuArgs.extend({
		"-kernel", KERNEL,
		"-drive", f"file={DISK_FILENAME},format=raw,index=0,media=disk",
		"-smp", options.get(opt.SMP, opt.SMP_DEFAULT),
		"-display", "gtk",
		"-device", "isa-debug-exit,iobase=0xf4,iosize=0x04",
//		"-d", "int",
		"-vga", "std",
		/* Enable the VM-based RTC clock, which pauses when the kernel is paused. */
		"-rtc", "base=utc,clock=vm"
	});
	local enableMonitorCommands = false;
	local monitorCommandPipe = none;
	if (enableMonitorCommands) {
		local final port = "1235";
		qemuArgs.extend({ "-monitor", "tcp::" + port + ",server" });
		local r;
		r, monitorCommandPipe = ipc.Pipe.new()...;
		enableMonitorCommands = Thread(qemuMonitorBackend, ("localhost:" + port, r));
	}
	local cmdline = options.get(opt.CMDLINE);
	if (cmdline) {
		qemuArgs.append("-append");
		qemuArgs.append(cmdline);
	}
	print "qemu:", repr qemuArgs;
	if (qemu is none) {
		qemu = common.findExecutableInPaths(
			enumerateQEmuInstallationLocations(),
			f"qemu-system-{TARGET_ARCH}");
		if (qemu is none)
			throw Error("Cannot locate qemu binary");
	}
	local proc = ipc.Process(qemu, qemuArgs);
	local r, w = ipc.Pipe.new()...;
	proc.stdout = w;
	proc.stderr = w;
	proc.start();
	if (enableMonitorCommands is Thread)
		enableMonitorCommands.start();
	try {
		w.close();
		/* XXX: Wait until qemu has opened up the port for the GDB stub! */
		magic.onEmulatorStarted();
		common.processKosDebugOutput(
			r: r,
			groups: groups,
			config: config,
			monitorCommandPipe: monitorCommandPipe,
			options: options);
		joinProcess(proc);
	} @[interrupt] catch (e...) {
		proc.terminate();
		throw;
	}
}
