/* Copyright (c) 2019-2022 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2019-2022 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */


import * from deemon;
import * from ..libmagic.libmagic;
import opt = ._options;
import fs;
import ipc;
import net;
import ._common as common;

function enumerateQEmuInstallationLocations(): {string...} {
#ifdef __WINDOWS__
	yield "D:\\qemu";
	yield "D:\\apps\\qemu";
#endif
	yield fs.environ["PATH"].split(fs.DELIM)...;
}

function qemuMonitorBackend(addr: string, monitorPipe: File) {
	local s = net.socket("INET", none, none);
	for (local i: [5000]) {
		try {
			s.connect(addr);
			break;
		} catch (e...) {
			if (i == 4999) {
				print Traceback.current;
				print repr e;
			}
		}
		Thread.sleep(milliseconds from time(1));
	}
	Thread([]{
		local isFirst = true;
		final local prefix = "monitor>: ";
		for (;;) {
			local text = s.recv();
			if (isFirst) {
				text = prefix + text;
				isFirst = false;
			}
			text = text.replace("\n", "\n" + prefix);
			File.stdout << text;
			File.stdout.sync();
		}
	}).start();
	local isFirst = true;
	final local prefix = "monitor<: ";
	for (;;) {
		local text = monitorPipe.read();
		s.send(text);
		if (isFirst) {
			text = prefix + text;
			isFirst = false;
		}
		text = text.replace("\n", "\n" + prefix);
		File.stdout << text;
		File.stdout.sync();
	}
}


function start(
		groups: {Group...},
		config: Config,
		BINPATH: string,
		KERNEL: string,
		DRIVERS: {(string, string)...},
		TARGET_ARCH: string,
		TARGET: Module,
		DISK_FILENAME: string,
		options: {string: string},
		magic: Module) {
	local qemu = options.get("qemu.exe");
	if (qemu is none) {
		qemu = common.findExecutableInPaths(
			enumerateQEmuInstallationLocations(),
			"qemu-system-" + TARGET_ARCH);
		if (qemu is none)
			throw Error("Cannot locate qemu binary");
	}
	local qemu_arguments = [];
	qemu_arguments.extend({ "-serial", "stdio" });
	if (opt.getBool(options, opt.HOSTED, true)) {
		/* NOTE: Qemu will refuse to guest requests for a reboot when
		 *       -no-reboot is given. However, reboot-on-tripple-fault
		 *       will  also  work when  -no-reboot isn't  given, which
		 *       probably isn't something you'd want...
		 * NOTE: When GDB is enabled, you'll get a SIGQUIT trap when
		 *       the guest tries to  reboot, and -no-reboot is  set. */
//		qemu_arguments.extend({ "-no-shutdown" });
		qemu_arguments.extend({ "-no-reboot", "-no-shutdown" });
	}
	local gdb_mode = options.get(opt.GDB);
	local boot_drivers = List(DRIVERS);
	if (gdb_mode !is none) {
		local gdb_port = options.get(opt.GDB_PORT, "1234");
		if (gdb_mode == "server") {
			/* Make use of out GDB server driver */
			boot_drivers.append((
				BINPATH + "/os/drivers/gdbserver",
				"transport=serial:0x2f8:0xf3"
			));
			qemu_arguments.extend({ "-serial", "tcp::" + gdb_port + ",server" });
		} else if (gdb_mode == "emulator") {
			/* Use qemu's builtin GDB support */
			/* Make use of out GDB-stub driver */
			if (gdb_port != "1234") {
				qemu_arguments.extend({ "-gdb", "tcp::" + gdb_port });
			} else {
				qemu_arguments.append("-s");
			}
			qemu_arguments.append("-S");
		} else {
			throw Error("Invalid GDB mode: " + repr gdb_mode);
		}
	}
	if (TARGET_ARCH == "x86_64") {
		/* Must create+use a custom kernel binary with e_machine = EM_386 */
		local kernel386 = "{}.em386-{}".format({
			fs.headof(KERNEL),
			fs.tailof(KERNEL),
		});
		local x32modified = getLastModfiedTime(kernel386);
		if (x32modified !is none) {
			local x64modified = getLastModfiedTime(KERNEL);
			if (x64modified !is none && x64modified <= x32modified)
				goto did_update_x86_64_kernel;
			try fs.unlink(kernel386); catch (...);
		}
		print "[qemu] Updating kernel shadow", repr kernel386;
		fs.copyfile(KERNEL, kernel386);
		with (local fp = File.open(kernel386, "rb+,nobuf")) {
			fp.seek(18); /* __OFFSET_ELF64_EHDR_MACHINE */
			fp.write((3).tobytes(2, "little", false)); /* EM_386 */
		}
did_update_x86_64_kernel:
		KERNEL = kernel386;
	}
	if (boot_drivers) {
		qemu_arguments.append("-initrd");
		qemu_arguments.append(",".join(
			for (local name, cmdline: boot_drivers)
				!cmdline
					? name.replace(",", ",,")
					: "{} {}".format({
						name.replace(",", ",,"),
						cmdline.replace(",", ",,")
					})
		));
	}
	try {
		final local trace_filename = "qemu.events";
		local trace_file = File.open(trace_filename, "r");
		for (local l: trace_file) {
			l = l.strip();
			if (l.startswith("#") || !l)
				continue;
			/* Non-empty trace file */
			qemu_arguments.extend({
				"-trace",
				"events=" + trace_filename
			});
			break;
		}
	} catch (...) {
	}
	for (local l: (try File.open("qemu.conf", "r") catch (...) none)) {
		l = l.strip();
		if (l.startswith("#") || !l)
			continue;
		l = l.decode("utf-8");
		local cmd, none, arg = l.partition("=")...;
		switch (cmd) {
		case "accel":
			qemu_arguments.extend({ "-accel", arg });
			break;
		case "xdrive1":
		case "xdrive2":
		case "xdrive3":
			if (!fs.stat.exists(arg))
				break;
			/* FALLTHRU */
		case "drive1":
		case "drive2":
		case "drive3":
			qemu_arguments.extend({
				"-drive",
				"file={},format=raw,index={},media=disk"
					.format({ arg, cmd.last })
			});
			break;
		case "args":
			qemu_arguments.extend(arg.split(" "));
			break;
		default:
			throw Error("Unknown qemu conf option: {!r}".format({ cmd }));
			break;
		}
		break;
	}
	qemu_arguments.extend({
		"-kernel", KERNEL,
		"-drive", "file=" + DISK_FILENAME + ",format=raw,index=0,media=disk",
		"-smp", options.get(opt.SMP, opt.SMP_DEFAULT),
		"-display", "gtk",
		"-device", "isa-debug-exit,iobase=0xf4,iosize=0x04",
//		"-d", "int",
		"-vga", "std",
		/* Enable the VM-based RTC clock, which pauses when the kernel is paused. */
		"-rtc", "base=utc,clock=vm"
	});
	local enableMonitorCommands = false;
	local monitorCommandPipe = none;
	if (enableMonitorCommands) {
		final local port = "1235";
		qemu_arguments.extend({ "-monitor", "tcp::" + port + ",server" });
		local r;
		r, monitorCommandPipe = ipc.Pipe.new()...;
		enableMonitorCommands = Thread(qemuMonitorBackend, ("localhost:" + port, r));
	}
	local cmdline = options.get(opt.CMDLINE);
	if (cmdline) {
		qemu_arguments.append("-append");
		qemu_arguments.append(cmdline);
	}
	print "qemu:", repr qemu_arguments;
	local proc = ipc.Process(qemu, qemu_arguments);
	local r, w = ipc.Pipe.new()...;
	proc.stdout = w;
	proc.stderr = w;
	proc.start();
	if (enableMonitorCommands is Thread)
		enableMonitorCommands.start();
	try {
		w.close();
		/* XXX: Wait until qemu has opened up the port for the GDB stub! */
		magic.onEmulatorStarted();
		common.processKosDebugOutput(
			r: r,
			groups: groups,
			config: config,
			monitorCommandPipe: monitorCommandPipe,
			options: options);
		joinProcess(proc);
	} @[interrupt] catch (e...) {
		proc.terminate();
		throw;
	}
}
