final global COPYRIGHT=
"/* Copyright (c) 2019-2022 Griefer@Work                                       *
 *                                                                            *
 * This software is provided 'as-is', without any express or implied          *
 * warranty. In no event will the authors be held liable for any damages      *
 * arising from the use of this software.                                     *
 *                                                                            *
 * Permission is granted to anyone to use this software for any purpose,      *
 * including commercial applications, and to alter it and redistribute it     *
 * freely, subject to the following restrictions:                             *
 *                                                                            *
 * 1. The origin of this software must not be misrepresented; you must not    *
 *    claim that you wrote the original software. If you use this software    *
 *    in a product, an acknowledgement (see the following) in the product     *
 *    documentation is required:                                              *
 *    Portions Copyright (c) 2019-2022 Griefer@Work                           *
 * 2. Altered source versions must be plainly marked as such, and must not be *
 *    misrepresented as being the original software.                          *
 * 3. This notice may not be removed or altered from any source distribution. *
 */";

#ifdef __WINDOWS__
#define HOST_EXE(x)  x ".exe"
#else
#define HOST_EXE(x)  x
#endif

import fs;
import ipc;
import util;
import * from deemon;
import * from .kos.misc.libmagic.libmagic;
import * from .kos.misc.libmagic.steps.c;
import * from .kos.misc.libmagic.steps.mtools;
import * from .kos.misc.libmagic.steps.util;
import * from .kos.misc.libmagic.steps.elf;
import * from .kos.misc.libmagic.steps.elf_dilink;
import * from .kos.misc.libmagic.steps.custom;
import * from .kos.misc.libmagic.steps.keymap;
import * from .kos.misc.libmagic.toolchain.gcc;
import .kos.misc.magicemulator._options as emulatorOptions;
import AppExit from errors;

File.stdout.setbuf("line");
File.stderr.setbuf("line");

global CONFIG_MAX_COMPILERS: int = none;
global CONFIG_EMULATOR_USED: string = "qemu"; /* none or "qemu" / "bochs" */
global CONFIG_EMULATOR_OPTIONS: {string: string} = Dict();
global CONFIG_DONT_BUILD: bool = false;
global CONFIG_ONLY_BUILD_LIBRARIES: bool = false;
global CONFIG_EMULATOR_STARTED_PATTERN: string = none;
global CONFIG_EMULATOR_GDB_MODE: string = none;
global CONFIG_MULTIBOOT_DRIVERS: {(string, string)...} = List();
global requiredOutputWhiteList: {(bool, string)...} | none = none;
global requiredGroupWhiteList: {string...} | none = none;
global requiredGroupOfSourceFileWhiteList: {string...} | none = none;
global config: Config = Config(LIBRARY_SUFFIX: ".so");
global toolchain: Toolchain = GCCToolchain(config: config);
global toolchainsAndGroups: {(Toolchain, {Group...})...};
global groups: {Group...} = [];
global options: {string: Object} = Dict();

/* Build configuration */
global CONFIG_OPTIMIZE: bool;
global CONFIG_DEBUG: bool;

/* Target configuration */
global TARGET_REAL_ARCH: string; /* Real target architecture (e.g. "i386") */
global TARGET_ARCH: string;      /* Target architecture (e.g. "i386") */
global TARGET_NAME: string;      /* Target configuration name (e.g. "i386-kos-OD") */
global TARGET: Module = none;    /* Target configuration module */
global TARGET_BINPATH: string;   /* Target binpath (e.g. "/bin/i386-kos-OD") */
global KERNEL_BINARY: string;

@@Called by emulators once the emulator has been started.
@@Preferrably called _after_ a GDB stub (if any) becomes available.
function onEmulatorStarted() {
	assert CONFIG_EMULATOR_STARTED_PATTERN !== false;
	if (CONFIG_EMULATOR_STARTED_PATTERN)
		print CONFIG_EMULATOR_STARTED_PATTERN;
	CONFIG_EMULATOR_STARTED_PATTERN = false;
}

function parseConfig(config: string): (bool, bool) {
	return {
		"OD" : (true, true),
		"OnD" : (true, false),
		"nOD" : (false, true),
		"nOnD" : (false, false),
	}[config];
}

function setConfig(config: string) {
	CONFIG_OPTIMIZE, CONFIG_DEBUG = parseConfig(config)...;
}

function loadMissingSettings() {
	local target, config =
		import(".kos.misc.scripts.vsautoconfig")
			.parseCurrentVsConfiguration("kos/.vs/ProjectSettings.json")...;
	print "Automatically loaded settings:", repr target, repr config;
	if (TARGET_ARCH !is bound)
		TARGET_ARCH = target;
	local config_optimize, config_debug = parseConfig(config)...;
	if (CONFIG_OPTIMIZE !is bound)
		CONFIG_OPTIMIZE = config_optimize;
	if (CONFIG_DEBUG !is bound)
		CONFIG_DEBUG = config_debug;
}


function getConfig(): string {
	return "{}{}".format({
		CONFIG_OPTIMIZE ? "O" : "nO",
		CONFIG_DEBUG ? "D" : "nD",
	});
}

function generateFile(filename: string, isRegEx: bool) {
	if (requiredOutputWhiteList is none)
		requiredOutputWhiteList = HashSet();
	if (!isRegEx) {
		if (filename.startswith("/"))
			filename = fs.abspath(filename.lstrip("/"), ROOTDIR);
		else {
			filename = fs.abspath(filename);
		}
		filename = normalizePath(filename, false);
	}
	requiredOutputWhiteList.insert((isRegEx, filename));
	CONFIG_EMULATOR_USED = none;
}

function generateGroup(name: string) {
	if (requiredGroupWhiteList is none)
		requiredGroupWhiteList = HashSet();
	requiredGroupWhiteList.insert(name);
	CONFIG_EMULATOR_USED = none;
}

function generateProjectOfInputFile(filename: string) {
	if (requiredGroupOfSourceFileWhiteList is none)
		requiredGroupOfSourceFileWhiteList = HashSet();
	filename = filename.startswith("/")
		? fs.abspath(filename.lstrip("/"), ROOTDIR)
		: fs.abspath(filename);
	filename = normalizePath(filename, false);
	requiredGroupOfSourceFileWhiteList.insert(filename);
	CONFIG_EMULATOR_USED = none;
}

function buildToolchainsAndGroups() {
	if (CONFIG_MAX_COMPILERS is none)
		CONFIG_MAX_COMPILERS = try import("posix").cpu_count() catch (...) 1;
	if (requiredOutputWhiteList !is none) {
		return buildGroupsWithOutputWhitelist(
			toolchainsAndGroups: toolchainsAndGroups,
			outputWhiteList:     requiredOutputWhiteList,
			maxParallelSteps:    CONFIG_MAX_COMPILERS);
	} else if (requiredGroupWhiteList !is none) {
		return buildGroupsWithGroupWhitelist(
			toolchainsAndGroups: toolchainsAndGroups,
			groupWhiteList:      requiredGroupWhiteList,
			maxParallelSteps:    CONFIG_MAX_COMPILERS);
	} else if (requiredGroupOfSourceFileWhiteList !is none) {
		return buildGroupsWithGroupOfInputFileWhitelist(
			toolchainsAndGroups: toolchainsAndGroups,
			inputFileWhitelist:  requiredGroupOfSourceFileWhiteList,
			maxParallelSteps:    CONFIG_MAX_COMPILERS);
	} else {
		return buildGroups(
			toolchainsAndGroups: toolchainsAndGroups,
			maxParallelSteps:    CONFIG_MAX_COMPILERS);
	}
}

function setBinTargetLink(target_arch: string, target_name: string) {
	/* Make sure that the correct bin-config is selected. */
	local selConfigFile = "bin/{}-kos".format({ target_arch });
	local selConfig = readlink(selConfigFile);
	if (selConfig !is string || selConfig != target_name) {
		if (selConfig !is none) {
			try {
				fs.unlink(selConfigFile);
			} catch (...) {
			}
		}
		print("Changing libpath: `ln -s ", repr(target_name), " ", repr(selConfigFile), "`");
		local p = ipc.Process(HOST_EXE("bash"), [ "-c",
			"ln -s {!r} {!r}".format({ target_name, selConfigFile })
		]);
		p.start();
		joinProcess(p);
	}
}

function build(reconfigure: Callable = none, binTargetName: string = none) {
	TARGET_NAME = "{}-kos-{}".format({ TARGET_ARCH, getConfig() });
	if (binTargetName is none)
		binTargetName = TARGET_NAME;
	setBinTargetLink(TARGET_ARCH, binTargetName);

	/* To speed up compilation, provide some known-good values
	 * for standard library paths valid for the KOS  toolchain */
	TARGET.provideToolchainCache(toolchain, TARGET_NAME);

	/* Apply target configurations. */
	config.CROSS_PREFIX = TARGET.CROSS_PREFIX;
	config.BUILD_PATH = "build/" + TARGET_NAME;
	TARGET_BINPATH = "/bin/" + binTargetName;
	KERNEL_BINARY  = TARGET_BINPATH + "/os/kernel.bin";

	/* Configure MTOOLS build steps */
	toolchain.options[TOOLCHAIN_MTOOLS_EXE]  = HOST_EXE("binutils/misc/bin/mtools");
	toolchain.options[TOOLCHAIN_MTOOLS_DISK] = TARGET_BINPATH[1:] + "/disk.img";

	import . as me;
	reconfigure(me);

	{
		final local TARGET = __nth(2) TARGET;
		final local XARCH  = TARGET.XARCH;
		final local SUFFIX = TARGET.SUFFIX;
		final local ASMFILES = TARGET.ASMFILES;
		final local XARCH_ASMFILES = "{}/{}".format({ XARCH, ASMFILES });
		final local DISK_LIBPATH = TARGET.DISK_LIBPATH;
		final local DISK_USRLIBPATH = TARGET.DISK_USRLIBPATH;
#define BEGIN           { local options = deepcopy __nth(2) options;
#define BEGIN_ANONYMOUS { local options = Dict();
#define END             }
#include "kos/.sources"
	}

	toolchainsAndGroups = { (toolchain, groups) };

	/* NOTE: Must always load groups for stuff like like magic-driven %{vinfo} */
	if (CONFIG_DONT_BUILD)
		return;

	/* Check that source generators are up-to-date. */
	import updateAutomaticSourcesOnDemand from .kos.misc.magicgenerator.ondemand;
	updateAutomaticSourcesOnDemand(TARGET_ARCH, TARGET);

	/* Automatically re-build changed sources */
	buildToolchainsAndGroups();
}

function main(argv: {string...}) {
	local args = List(argv);
	print repr args;
	while (args) {
		local arg = args.pop(0);
		switch (arg) {
		case "-1":
			CONFIG_MAX_COMPILERS = 1;
			break;
		case "-v":
		case "--verbose":
			config.VERBOSE = true;
			break;
		case "-E":
			options.setdefault(OPT_COMPILE_SECONDARY, []).append("E");
			CONFIG_EMULATOR_USED = none;
			break;
		case "-S":
			options.setdefault(OPT_COMPILE_SECONDARY, []).append("S");
			CONFIG_EMULATOR_USED = none;
			break;
		case "--run-only":
			CONFIG_DONT_BUILD = true;
			break;
		case "--libraries-only":
			CONFIG_ONLY_BUILD_LIBRARIES = true;
			break;
		case "--build-only":
			CONFIG_EMULATOR_USED = none;
			break;
		case "-f":
			config.FORCE = true;
			break;
		case "--emulator-hosted":
			CONFIG_EMULATOR_OPTIONS[emulatorOptions.HOSTED] = "true";
			break;
		case "--format-error-messages":
			config.FORMAT_ERRORS = true;
			break;
		case "--":
			CONFIG_EMULATOR_OPTIONS[emulatorOptions.CMDLINE] = " ".join(args);
			args = { };
			break;
		default:
			if ("=" in arg) {
				local name, none, value = arg.partition("=")...;
				local final BOOLMAP = { "0": false, "1": true };
				switch (name) {
				case "--echo":
					print "ECHO:", repr(value);
					break;
				case "--feature-test":
					import(".kos.misc.libmagic.featuretest")
						.generateCompilerSpecs(toolchain, value, COPYRIGHT);
					AppExit.exit(0);
					break;
				case "-n":
					CONFIG_MAX_COMPILERS = int(value);
					break;
				case "--emulator-setopt": {
					if ("=" !in value)
						throw Error("--emulator-setopt requires the operand to contain \"=\", which isn't the case for " + repr(value));
					local optnam, none, optval = value.partition("=")...;
					CONFIG_EMULATOR_OPTIONS[optnam] = optval;
				}	break;
				case "--emulator":
					CONFIG_EMULATOR_USED = value;
					break;
				case "--changed":
					setForceChanged(normalizePath(value, true));
					break;
				case "--verbose":
					config.VERBOSE = BOOLMAP[value];
					break;
				case "--gdb":
					CONFIG_EMULATOR_OPTIONS[emulatorOptions.GDB] = value;
					break;
				case "--emulator-started-pattern":
					CONFIG_EMULATOR_STARTED_PATTERN = value;
					break;
				case "--target":
					TARGET_ARCH = value;
					break;
				case "--config":
					setConfig(value);
					break;
				case "--vsgengroup":
					config.FORMAT_ERRORS = true;
				case "--gengroup":
					generateGroup(value);
					break;
				case "--vcautotargetconfig": {
					local target, config =
						import(".kos.misc.scripts.vsautoconfig")
						.parseCurrentVsConfiguration(value)...;
					TARGET_ARCH = target;
					setConfig(config);
				}	break;

				case "--vsgengroup-of":
					config.FORMAT_ERRORS = true;
				case "--gengroup-of":
					generateProjectOfInputFile(value);
					break;

				case "--vsgen":
					config.FORMAT_ERRORS = true;
				case "--gen":
					generateFile(value, false);
					break;

				case "--vsregen":
					config.FORMAT_ERRORS = true;
				case "--regen":
					generateFile(value, true);
					break;

				case "--driver": {
					for (local opt: value.split(":")) {
						local name = opt, cmdline = "";
						if ("," in opt)
							name, none, cmdline = opt.partition(",")...;
						CONFIG_MULTIBOOT_DRIVERS.append((name, cmdline));
					}
				}	break;

				case "--clang-tidy":
					options.setdefault(OPT_COMPILE_SECONDARY, [])
						.append("clang-tidy=" + value);
					/* Configure clang-tidy macros and options. */
					{
						local mac = options.setdefault(OPT_COMPILE_MACROS, Dict());
						mac["__KOS__"] = "1";
						mac["__ELF__"] = "1";
						if (TARGET_ARCH !is bound)
							loadMissingSettings();
						mac["__" + TARGET_ARCH + "__"] = "1";
					}
					options.setdefault(OPT_COMPILE_CLANG_TIDY_CC_ARGS, []).extend({
						"-nostdinc",
						"-isystem", "kos/include/i386-kos", /* FIXME: Don't hardcode here! */
						"-isystem", "kos/include",
					});
					CONFIG_EMULATOR_USED = none;
					break;

				case "--lint":
					options.setdefault(OPT_COMPILE_SECONDARY, [])
						.append("lint=" + value);
					/* Configure clang-tidy macros and options. */
					{
						local mac = options.setdefault(OPT_COMPILE_MACROS, Dict());
						mac["__KOS__"] = "1";
						mac["__ELF__"] = "1";
						if (TARGET_ARCH !is bound)
							loadMissingSettings();
						mac["__" + TARGET_ARCH + "__"] = "1";
					}
					options.setdefault(OPT_COMPILE_LINT_ARGS, []).extend({
						"-Ikos/include/i386-kos", /* FIXME: Don't hardcode here! */
						"-Ikos/include",
					});
					CONFIG_EMULATOR_USED = none;
					break;

				default:
					goto _err_unknown_argument;
				}
			} else {
_err_unknown_argument:
				print "Unknown argument", repr arg;
				AppExit.exit(1);
			}
			break;
		}
	}
	fs.chdir(ROOTDIR);
	if (CONFIG_OPTIMIZE !is bound || CONFIG_DEBUG !is bound || TARGET_ARCH !is bound)
		loadMissingSettings();

	TARGET_REAL_ARCH = TARGET_ARCH;
	TARGET = import(".kos.misc.targets." + TARGET_ARCH);
	/* Build everything that was selected. */
	if (!CONFIG_DONT_BUILD) {
		if (hasattr(TARGET, "build")) {
			import . as me;
			TARGET.build(me);
		} else {
			build();
		}
	} else {
		build();
	}

	/* Start KOS with the selected emulator (if any). */
	if (CONFIG_EMULATOR_USED !is none) {
		/* Driver name without any slashes are automatically
		 * loaded   from    the    /os/drivers    directory. */
		for (local i, elem: util.enumerate(CONFIG_MULTIBOOT_DRIVERS)) {
			local name, cmdline = elem...;
			if ("/" !in name && "\\" !in name) {
				name = "bin/{}/os/drivers/{}".format({ TARGET_NAME, name });
				CONFIG_MULTIBOOT_DRIVERS[i] = (name, cmdline);
			}
		}
		import . as me;
		import(".kos.misc.magicemulator." + CONFIG_EMULATOR_USED).start(
			groups:        groups,
			config:        config,
			BINPATH:       TARGET_BINPATH[1:],
			KERNEL:        KERNEL_BINARY[1:],
			DRIVERS:       CONFIG_MULTIBOOT_DRIVERS,
			TARGET_ARCH:   TARGET_ARCH,
			TARGET:        TARGET,
			DISK_FILENAME: toolchain.options[TOOLCHAIN_MTOOLS_DISK],
			options:       CONFIG_EMULATOR_OPTIONS,
			magic:         me
		);
	}
}

#if defined(__MAIN__) && __BASE_FILE__ == __FILE__
try {
	main([...][1:]);
} @[interrupt] catch (e...) {
	print e;
	print repr Traceback.current;
	File.Buffer.sync();
	print "ERROR";
	AppExit.exit(1);
}
File.Buffer.sync();
print "DONE";
AppExit.exit(0);
#endif
